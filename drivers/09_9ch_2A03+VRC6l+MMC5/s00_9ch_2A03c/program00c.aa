;;;;;;;;;;;;;;;;
;	[setting]
;;;;;;;;;;;;;;;;
\setophead	0		;set whether to output header, 0 for data module (def=1)
\setoptail	1		;set whether to output dummy data to make link possible (def=0)
\setdefnumlen	1		;set default length for number (command $ and #) (def=1)

;;;;;;;;;;;;;;;;
;	[system]
;;;;;;;;;;;;;;;;
	;bank switch
	@5FF6
1:BANK6
1:BANK7
1:BANK8
1:BANK9
1:BANKA
1:BANKB
1:BANKC
1:BANKD
1:BANKE
1:BANKF
	;multiplier in MMC5
	@5205
1:MULLO		;store data to MULLO and MULHI then read the product
1:MULHI

;;;;;;;;;;;;;;;;
;	[apu]
;;;;;;;;;;;;;;;;
;;;;	[2A03]	;channel=rec1,rec2,tri,noise,dpcm
	;general
	@4015
:ST2A03		;-,-,-,dpcm,noise,tri,rec2,rec1 enable
	@4017
:IRQ2A03	;framemode,IRQ,-,-,-,-,-,-
	;rec1
	@4000
1:REC1VOL	;duty(2),lenhlt,~env,vol/envPeriod(4)
1:REC1SWP	;sweep,period(3),negate,shift(3)
1:REC1LOPERIOD	;loPeriod(8)
1:REC1HIPERIOD	;length(5),hiPeriod(3)
	;rec2
1:REC2VOL
1:REC2SWP
1:REC2LOPERIOD
1:REC2HIPERIOD
	;tri
2:TRILINC	;lenhlt,linarcount(7)
1:TRILOPERIOD	;loPeriod(8)
1:TRIHIPERIOD	;length(5),hiPeriod(3)
	;noise
2:NOISEVOL	;-,-,lenhlt,1,vol(4)
1:NOISEPERIOD	;short,-,-,-,period(4)
1:NOISELEN	;length(5),-,-,-
	;dpcm
1:DPCMMODE	;IRQ,loop,-,-,freq(4)
1:DPCMDATA	;-,data(7)
1:DPCMWAVE	;addr(8)
1:DPCMLEN	;length(8)

;;;;;;;;;;;;;;;;
;	[linked_data]
;;;;;;;;;;;;;;;;
;;;;	[bank8]	;config
	@8100
256:MAINPAGE
	@8200
256:PAGEEND
	@8490
1:INITTEMPOMUL
1:INITTEMPODIV
1:INITLOOPINDEX
1:INITLOOPADDR
1:INITDELAY
1:NOISEINITVOL
	@849C
1:REC1INITINSVOL
1:REC2INITINSVOL
	@8800
128:DIVHIPERIOD
128:DIVLOPERIOD
	@8900
:INITCALLBACK

;;;;	[bankB]	;data main
	@B000
256:DPCMNOTE
256:NOISENOTE
256:TRINOTE
256:REC5NOTE
256:REC6NOTE
256:REC1NOTE
256:REC2NOTE
256:REC3NOTE
256:REC4NOTE
256:NOISEINSVOL
256:REC5INSVOL
256:REC6INSVOL
256:REC1INSVOL
256:REC2INSVOL
256:REC3INSVOL
256:REC4INSVOL

;;;;	[bankD]	;sample
	@DFFE
1:SAMPLESPEED
1:SAMPLELENGTH

;;;;;;;;;;;;;;;;
;	[zeropage]
;;;;;;;;;;;;;;;;
@00
1:STATE 1:COUNT 1:TEMPOMUL 1:TEMPODIV 1:LOOPINDEX 1:LOOPADDR 1:PAGEINDEX 1:ADDR 1:MAXADDR
@20
1:REC1INSBUF 1:REC2INSBUF
@30
1:REC1VOLBUF 1:REC2VOLBUF
@F0
1:=$70

;;;;;;;;;;;;;;;;
;	[code]
;;;;;;;;;;;;;;;;
@9000
:_START_
@9011
:_INIT_ {
	JMP &INIT
}

@9031
:_PLAY_ {
	LD %STATE BNZ #1 RET
	BPZ #11
		LD %COUNT BZE #3
			DEC %COUNT
			RET
		;end if
		LI $01 ST %STATE
	;end if
	LD %COUNT SEC SBC %TEMPOMUL ST %COUNT BCC #1 RET
	ADC %TEMPODIV ST %COUNT
	LDY %ADDR
	LD,Y &NOISEINSVOL ANDI $0F
	BZE #5
		ORI $30 ST &NOISEVOL
	;end if
	LD,Y &NOISENOTE
	CMPI $FE BNE #12
		LI $07 ST &ST2A03
		LI $0F ST &ST2A03
	BNZ #12 BIT %=$70 BNZ #8	;BNZ for BRA
		ST &NOISEPERIOD
		LI $00 ST &NOISELEN
	;end else if
	LD,Y &TRINOTE TAX
	ANDI $0F CMPI $0C BGE #14
		LD,X &DIVHIPERIOD ST &TRIHIPERIOD
		LD,X &DIVLOPERIOD ST &TRILOPERIOD
	BLO #14 CMPI $0E BNE #10	;BLO for BRA
		LI $0B ST &ST2A03
		LI $0F ST &ST2A03
	;end else if
	LD,Y &REC1INSVOL ANDI $0F
	BZE #7
		ST %REC1VOLBUF
		OR %REC1INSBUF ST &REC1VOL
	;end if
	LD,Y &REC1INSVOL ANDI $F0
	BZE #9
		ORI $30 ST %REC1INSBUF
		OR %REC1VOLBUF ST &REC1VOL
	;end if
	LD,Y &REC1NOTE TAX
	ANDI $0F CMPI $0C BGE #14
		LD,X &DIVHIPERIOD ST &REC1HIPERIOD
		LD,X &DIVLOPERIOD ST &REC1LOPERIOD
	BLO #14 CMPI $0E BNE #10	;BLO for BRA
		LI $0E ST &ST2A03
		LI $0F ST &ST2A03
	;end else if
	LD,Y &REC2INSVOL ANDI $0F
	BZE #7
		ST %REC2VOLBUF
		OR %REC2INSBUF ST &REC2VOL
	;end if
	LD,Y &REC2INSVOL ANDI $F0
	BZE #9
		ORI $30 ST %REC2INSBUF
		OR %REC2VOLBUF ST &REC2VOL
	;end if
	LD,Y &REC2NOTE TAX
	ANDI $0F CMPI $0C BGE #14
		LD,X &DIVHIPERIOD ST &REC2HIPERIOD
		LD,X &DIVLOPERIOD ST &REC2LOPERIOD
	BLO #14 CMPI $0E BNE #10	;BLO for BRA
		LI $0D ST &ST2A03
		LI $0F ST &ST2A03
	;end else if
	INC %ADDR LD %ADDR
	BZE #6 CLC SBC %MAXADDR BCS #1 RET
	INC %PAGEINDEX LDX %PAGEINDEX
}

:NEWPAGE {
	LIY $00
	LD,X &MAINPAGE BNZ #15
		LDX %LOOPINDEX
		LD,X &MAINPAGE BNZ #5
			LI $00 ST %STATE
			RET
		;end if
		LDY &LOOPADDR
	;end if
	STX %PAGEINDEX
	ST &BANKB
	STY %ADDR
	LD,X &PAGEEND ST %MAXADDR
RET }

:INIT {
	(
	LI $00 ST &ST2A03
	LI $08 ST &REC1SWP ST &REC2SWP
	LD &REC1INITINSVOL ORI $30 TAX ST &REC1VOL
	ANDI $0F ST %REC1VOLBUF
	TXA ANDI $F0 ST %REC1INSBUF
	LD &REC2INITINSVOL ORI $30 TAX ST &REC2VOL
	ANDI $0F ST %REC2VOLBUF
	TXA ANDI $F0 ST %REC2INSBUF
	LI $81 ST &TRILINC
	LD &NOISEINITVOL ANDI $0F ORI $30 ST &NOISEVOL
	LI $0F ST &ST2A03
	LI #-1 ST %STATE
	LD &INITDELAY ST %COUNT
	LD &INITTEMPOMUL ST %TEMPOMUL
	LD &INITTEMPODIV ST %TEMPODIV
	LD &INITLOOPINDEX ST %LOOPINDEX
	LD &INITLOOPADDR ST %LOOPADDR
	LIX $00
	)
	CALL &INITCALLBACK
	LI $70 ST %=$70
	JMP &NEWPAGE
}
